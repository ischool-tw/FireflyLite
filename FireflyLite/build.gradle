apply plugin: 'com.android.library'

//For Deploying to JCenter  (ref: http://www.jianshu.com/p/22a6d2ac99cc )
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "0.2.0"


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 2
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:support-v4:23.4.0'

}

//For deploying to JCenter .....

def siteUrl = 'https://github.com/ischool-tw/FireflyLite'    // Homepage URL of the library
def gitUrl = 'https://github.com/ischool-tw/FireflyLite.git' // Git repository URL
group = "tw.com.ischool"                               // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            artifactId = "FireflyLite"
            project {
                packaging 'aar'

                // Add your description here
                name 'Single Sign On to ischool account center.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kevin-huang'
                        name 'kevin'
                        email 'kevin.huang@ischool.com.tw'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'FireflyLite'
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        licenses = ['Apache-2.0']

        issueTrackerUrl = 'https://github.com/ischool-tw/FireflyLite/issues'
        publicDownloadNumbers = true
        version {
            name = '0.1.0'
            desc = 'An Android library you can use to SSO to ischool account center.'
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}